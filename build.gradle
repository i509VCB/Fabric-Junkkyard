plugins {
	id "com.jfrog.bintray" version "1.0"
	id 'fabric-loom' version '0.2.7-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		url "https://dl.bintray.com/i509vcb/maven"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_build}:v2"
	modCompile "net.fabricmc:fabric-loader:${project.loader_version}"

	modCompile "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	implementation "org.checkerframework:checker-qual:3.0.1"
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

if (file('private.gradle').exists()) { // For publishing
	apply(from: 'private.gradle')
}

bintray {
	if (ext.has("bintrayUsername") && ext.has("bintrayKey")) {
		user = property('bintrayUsername')
		key = property('bintrayKey')
		pkg {
			repo = 'generic'
			name = 'fabric-junkkyard'
			userOrg = user
			licenses = ['MIT']
			vcsUrl = 'https://github.com/i509VCB/Fabric-Junkkyard.git'
			version {
				name = project.version
			}
		}
	} else {
		println("Publishing failed, you are required to have a 'private.gradle' with your user and key!")
	}
}

publishing {
	publications {
		MyPublication(MavenPublication) {
			from components.java
			groupId 'me.i509'
			artifactId 'fabric-junkkyard'
			version project.version
		}
	}
}
